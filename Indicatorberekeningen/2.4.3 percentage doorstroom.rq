PREFIX vph: <https://purl.org/ozo/vph#>
PREFIX kik: <https://purl.org/ozo/kik#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT (SUM(?doorstroom) AS ?teller) ((SUM(?np_count)+SUM(?nv_count))/2 AS ?noemer)
(SUM(?doorstroom)/((SUM(?np_count)+SUM(?nv_count))/2)*100 AS ?indicator)

WHERE {
    #creeer variabelen om te kunnen sommeren
    #als er een niveau is op peildatum en in verleden en deze zijn ongelijk aan elkaar, dan doorstroom
    BIND (IF(isIRI(?np) && isIRI(?nv) && ?nv != ?np, "1"^^xsd:integer, "0"^^xsd:integer) AS ?doorstroom)
    BIND (IF(isIRI(?np), "1"^^xsd:integer, "0"^^xsd:integer) AS ?np_count)
    BIND (IF(isIRI(?nv), "1"^^xsd:integer, "0"^^xsd:integer) AS ?nv_count)
    {
        SELECT DISTINCT ?persoon (MIN(?niveau_peildatum) AS ?np) (MIN(?niveau_verleden) AS ?nv)

        WHERE {
            BIND ("2018-01-01"^^xsd:date AS ?peildatum)
            BIND (?peildatum - "P1Y"^^xsd:duration AS ?verleden)
            ?persoon a vph:Human ;
                    kik:hasAgreement ?overeenkomst .
            ?overeenkomst kik:hasQuality ?kwalificatie .
            ?kwalificatie kik:hasQualityValue ?kwalificatieniveau ;
                        kik:startDatum ?start_kwal_niveau .
            OPTIONAL {?kwalificatie kik:eindDatum ?eind_kwal_niveau}
            BIND(IF(?start_kwal_niveau <= ?peildatum && (?eind_kwal_niveau >= ?peildatum || !BOUND(?eind_kwal_niveau)), ?kwalificatieniveau, '') AS ?niveau_peildatum)
            BIND(IF(?start_kwal_niveau <= ?verleden && (?eind_kwal_niveau >= ?verleden || !BOUND(?eind_kwal_niveau)), ?kwalificatieniveau, '') AS ?niveau_verleden)
            

        } GROUP BY ?persoon
    }
}